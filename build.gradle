// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.versions = [
            'release'           : '3.1.0-SNAPSHOT',

            'minSdk'            : 14,
            'compileSdk'        : 26,
            'buildTools'        : '26.0.2',

            'supportLibrary'    : '26.0.0-alpha1',
            'androidPlugin'     : '2.3.3',
            'kotlin'            : '1.1.2-4',

            'butterknife'       : '8.7.0',
            'nineoldandroids'   : '2.4.0',
            'systembartint'     : '1.0.4',
            'commons_lang'      : '3.5',
            'commons_collection': '4.1',
            'gson'              : '2.8.0',
            'commons_io'        : '2.5',
            'retrofit'          : '2.3.0',
            'glide_okhttp3'     : '1.5.0@aar',
            'okhttp3'           : '3.8.0',
            'annotations_java5' : '15.0',
            'glide'             : '3.8.0',
            'dagger'            : '2.10',
            'guava'             : '22.0-android',
    ]

    ext.deps = [
            android   : [
                    'runtime'     : 'com.google.android:android:4.1.1.4',
                    'gradlePlugin': "com.android.tools.build:gradle:${versions.androidPlugin}",
            ],
            'support' : [
                    'compat_v7'      : "com.android.support:appcompat-v7:${versions.supportLibrary}",
                    'cardview_v7'    : "com.android.support:cardview-v7:${versions.supportLibrary}",
                    'recyclerview_v7': "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                    'annotations'    : "com.android.support:support-annotations:${versions.supportLibrary}",

            ],
            'library' : [
                    'guava'               : "com.google.guava:guava:${versions.guava}",
                    'butterknife'         : "com.jakewharton:butterknife:${versions.butterknife}",
                    'butterknife-compiler': "com.jakewharton:butterknife-compiler:${versions.butterknife}",
                    'nineoldandroids'     : "com.nineoldandroids:library:${versions.nineoldandroids}",
                    'systembartint'       : "com.readystatesoftware.systembartint:systembartint:${versions.systembartint}",
                    'commons_lang3'       : "org.apache.commons:commons-lang3:${versions.commons_lang}",
                    'collections4'        : "org.apache.commons:commons-collections4:${versions.commons_collection}",
                    'gson'                : "com.google.code.gson:gson:${versions.gson}",
                    'retrofit'            : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                    'glide_okhttp3'       : "com.github.bumptech.glide:okhttp3-integration:${versions.glide_okhttp3}",
                    'okhttp3'             : "com.squareup.okhttp3:okhttp:${versions.okhttp3}",
                    'java5'               : "org.jetbrains:annotations-java5:${versions.annotations_java5}",
                    'glide'               : "com.github.bumptech.glide:glide:${versions.glide}",
                    'dagger'              : "com.google.dagger:dagger:${versions.dagger}",
                    'guava'               : "com.google.guava:guava:${versions.guava}",
            ],
            'apt'     : [
                    'dagger_compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
            ],

            // 代码生成
            'auto'    : [
                    'service': 'com.google.auto.service:auto-service:1.0-rc3',
                    'common' : 'com.google.auto:auto-common:0.8',
            ],
            //插件
            'kotlin'  : [
                    'stdLibJre8': "org.jetbrains.kotlin:kotlin-stdlib-jre8:${versions.kotlin}",
            ],
            javaparser: 'com.github.javaparser:javaparser-core:3.2.8',
            javapoet  : 'com.squareup:javapoet:1.9.0',
            junit: 'junit:junit:4.12',
            truth: 'com.google.truth:truth:0.36',
            //引用
            'release' : [
                    'runtime' : "com.jincanshen:sky:${versions.release}",
                    'compiler': "com.jincanshen:sky-compiler:${versions.release}"
            ],


    ]

}


subprojects { project ->
    group = GROUP
    version = VERSION_NAME

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        //中央库-快照
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }

    }

    if (!project.name.equals('sky-gradle-plugin')) {
        apply plugin: 'checkstyle'

        task checkstyle(type: Checkstyle) {
            configFile rootProject.file('checkstyle.xml')
            source 'src/main/java'
            ignoreFailures false
            showViolations true
            include '**/*.java'

            classpath = files()
        }

        afterEvaluate {
            if (project.tasks.findByName('check')) {
                check.dependsOn('checkstyle')
            }
        }
    }

    buildscript {
        repositories {
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2/"
            }


        }
        dependencies {
            classpath deps.android.gradlePlugin
            classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        }
    }
}

